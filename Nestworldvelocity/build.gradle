plugins {
    id 'java'
    id 'eclipse'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
    id 'com.github.johnrengelman.shadow' version '8.1.1' // Додано shadowJar
    id("xyz.jpenilla.run-velocity") version "2.3.1"
}

group = 'com.skyblockdynamic.nestworld.velocity'
version = '1.4-websoket'

repositories {
    mavenCentral() // Додано mavenCentral для залежностей
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'

    // Prometheus Metrics
    implementation 'io.prometheus:simpleclient:0.16.0'
    implementation 'io.prometheus:simpleclient_httpserver:0.16.0'
    implementation 'io.prometheus:simpleclient_common:0.16.0'
}

tasks {
    runVelocity {
        velocityVersion("3.4.0-SNAPSHOT")
    }
    // Налаштування shadowJar
    shadowJar {
        archiveClassifier.set('') // Робить назву файлу чистою, без "-all"
        // Виключаємо залежності, які не потрібно включати
        dependencies {
            exclude(dependency('com.velocitypowered:velocity-api'))
        }
    }
    
    // Вказуємо, що build залежить від shadowJar
    build {
        dependsOn shadowJar
    }
}

def targetJavaVersion = 17
java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = ['version': project.version]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java {
    srcDir 'src/main/java'
    //srcDir generateTemplates.map { it.outputs }
}
// sourceSets.main.resources {
//     srcDir 'src/main/resources'
// }


project.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)
